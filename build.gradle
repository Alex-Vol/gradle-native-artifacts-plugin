buildscript {
    repositories {
        maven {
            name "SimpliVity Artifactory Repository"
            url artifactoryUrl
        }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.1.1')
    }
    configurations.classpath {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

group = 'me.sgeb.gradle'
version = currentVersion
status = 'Development'

ext.isSnapshot = version.endsWith('-SNAPSHOT')

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Native Artifacts Plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

ext.pomConfiguration = {
    project {
        name 'Gradle Native Artifacts Plugin'
        description 'A gradle plugin to package C/C++ native executables and ' +
                'libraries into artifacts publishable to Ivy or Maven, with ' +
                'support for transitive dependencies.'
        url 'https://github.com/sgeb/gradle-native-artifacts-plugin'
        inceptionYear '2014'

        scm {
            url 'https://github.com/sgeb/gradle-native-artifacts-plugin'
            connection 'scm:git:https://github.com/sgeb/gradle-native-artifacts-plugin.git'
            developerConnection 'scm:git:https://github.com/sgeb/gradle-native-artifacts-plugin.git'
        }

        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'sgeb'
                name 'Serge Gebhardt'
            }
        }
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives groovydocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") ||
            gradle.taskGraph.hasTask("install") }
    sign configurations.archives
}

// GRADLE-1285 - workaround to get <packaging>jar</packaging>
//               into the pom file
def pomPackagingElement = { root ->
    def children = root.asNode().children()
    def versionIndex = children.indexOf(children.find {it.name().localPart == 'version'})
    children.add(versionIndex + 1, new Node(null, 'packaging', 'jar'))
}

install {
    repositories {
        mavenInstaller {
            beforeDeployment { mavenDeployment ->
                signing.signPom(mavenDeployment)
            }

            pom(pomConfiguration)

            pom.withXml pomPackagingElement
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { mavenDeployment ->
                signing.signPom(mavenDeployment)
            }

            repository(url: "file://$rootDir/../maven_repo")
            pom(pomConfiguration)
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

tasks['uploadArchives'].mustRunAfter tasks.withType(Test)

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':uploadArchives'))
    {
        if (!taskGraph.hasTask(':test'))
        {
            throw new StopExecutionException('Test task must be run for releases that ' +
                    'will be uploaded.')
        }
    }
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId archivesBaseName
            from components.java

            artifact groovydocJar
            artifact sourcesJar
        }
    }
}

apply plugin: 'com.jfrog.artifactory'

artifactory {
    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.addEnvironmentProperty('artifact.build.date',new java.util.Date().toString())

    contextUrl = artifactoryContextUrl

    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = project.isSnapshot ? 'plugins-snapshot-local' : 'plugins-release-local'
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
            maven = true
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            // properties = ['qa.level': 'basic', 'dev.team' : 'core']
            publishPom = true // Publish generated POM files to Artifactory (true by default)
            publishIvy = true // Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repository {
            repoKey = 'repo'
            maven = true
        }
    }
}
